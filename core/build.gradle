uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name 'Jabit Core'
                artifactId = 'jabit-core'
                description 'A Java implementation of the Bitmessage protocol. This is the core part. You\'ll either need the networking and repositories modules, too, or implement your own.'
            }
        }
    }
}

configurations {
    testArtifacts.extendsFrom testRuntime
}

task testJar(type: Jar) {
    classifier = 'test'
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}

dependencies {
    compile 'org.slf4j:slf4j-api'
    compile 'ch.dissem.msgpack:msgpack:1.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.nhaarman:mockito-kotlin:1.5.0'
    testCompile project(':cryptography-bc')
}

def generatedResources = "${project.buildDir}/generated-resources/main"

sourceSets {
    main {
        output.dir(generatedResources, builtBy: 'generateVersionInfo')
    }
}
task('generateVersionInfo') {
    doLast {
        def dir = new File(generatedResources)
        if (!dir.exists()) {
            dir.mkdirs()
        }
        def file = new File(generatedResources, "version")
        file.write(project.version.toString())
    }
}
